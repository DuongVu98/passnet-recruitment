plugins {
    id 'org.springframework.boot' version '2.5.1'
    id 'io.spring.dependency-management' version '1.0.9.RELEASE'
    id 'java'
    id "com.google.protobuf" version "0.8.16"
    id 'com.diffplug.gradle.spotless' version '4.5.1'
}
group = 'com.iucse.passnet'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '12'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}
repositories {
    mavenCentral()
}
sourceSets {
    main {
        proto {
            srcDir 'src/main/resources/proto'
        }
        java {
            srcDirs 'src/main/java'
            srcDirs 'src/generated/main/java'
            srcDirs 'src/generated/main/grpc'
        }
    }
}
protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.7.0"
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:1.19.0"
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }
    generateProtoTasks.generatedFilesBaseDir = 'src/generated'
}

spotless {
    java {
        target 'src/*/java/**/*.java'
        prettier(['prettier': '2.0.5', 'prettier-plugin-java': '0.8.0']).config(['parser': 'java', 'tabWidth': 4, 'printWidth': 150, 'useTabs': true])
    }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-aop'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.greenrobot:eventbus:3.2.0'
    implementation 'org.postgresql:postgresql:42.2.22'
    implementation 'net.devh:grpc-spring-boot-starter:2.12.0.RELEASE'
    implementation 'org.springdoc:springdoc-openapi-ui:1.4.6'
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
}
test {
    useJUnitPlatform()
}

bootJar {
    archiveName 'PassnetRecruitment.jar'
    manifest {
        attributes(
                'Start-Class': 'com.iucse.passnet.recruitment.PassnetRecruitmentApplication'
        )
    }
}

task bootRunLocal(dependsOn: bootRun) {
    bootRun {
        args = ["--spring.profiles.active=local"]
    }
}

task format(dependsOn: spotlessJavaApply) {
}
